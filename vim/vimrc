" If we're in compatible mode, ensure that the 'C' option that disallows line
" continuations is stripped out, as they're heavily used in this
" configuration for readability; we'll put it back later
if &compatible
  let s:cpoptions_save = &cpoptions
  set cpoptions-=C
endif

" Use different keys for global and local leaders
if has('eval')
  let g:mapleader = '\'
  let g:maplocalleader = '_'
endif

" Load configuration files from ~/.vim/config or its analogue, before Pathogen
" loads all its directories into 'runtimepath'
runtime! config/*.vim

" If our version isn't ancient and Pathogen is available, call it to load all
" the plugins in .vim/bundle; these are saved as submodules
if v:version >= 701

  " Bootstrap Pathogen
  runtime bundle/pathogen/autoload/pathogen.vim

  " Run Pathogen to broaden 'realtimepath' for plugin code and help tags
  silent! call pathogen#infect()
  silent! call pathogen#helptags()

  " The 'sahara' colorscheme only works for dark backgrounds with 256 colors
  if has('syntax')
        \ && &background ==# 'dark'
        \ && (has('gui_running') || &t_Co == 256)
    silent! colorscheme sahara
  endif
endif

" If we're in compatible mode, put 'cpoptions' back the way we found it at the
" start of this configuration, even though it's the current year
if exists('s:cpoptions_save')
  let &cpoptions = s:cpoptions_save
  unlet s:cpoptions_save
endif
